apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.app.kotlin_android_architecture_components"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }


    applicationVariants.all { variant -> renameArtifact(variant, defaultConfig) }


    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", "\"http://192.168.2.91/2017/\""
        }

        debug {
            buildConfigField "String", "SERVER_URL", "\"https://api.myjson.com/bins/\""
        }
    }

    dataBinding {
        enabled true
    }

    androidExtensions {
        experimental = true
    }
}

def renameArtifact(variant, defaultConfig) {
    variant.outputs.all {
        def appName
        if (project.hasProperty("applicationName")) {
            appName = applicationName
        } else {
            appName = parent.name
        }
        def formattedDate = new Date().format('yyyy_MM_dd')
        outputFileName = "${appName}_${variant.buildType.name}_${defaultConfig.versionName} (Date ${formattedDate}).apk"
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin_gradle_plugin"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraint_layout"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.recyclerview:recyclerview:$versions.recyclerview"
    implementation "com.google.android.material:material:$versions.material"

    kapt "com.android.databinding:compiler:$versions.databinding"

    // liveData and viewModel
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle_extensions"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp_logging_interceptor"
    implementation "com.github.bumptech.glide:glide:$versions.glide"

    testImplementation "junit:junit:$versions.junit"
    androidTestImplementation "androidx.test:runner:$versions.runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"
}
